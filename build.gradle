buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies { classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT' }
}

apply plugin: 'forge'
apply plugin: 'maven-publish'
apply plugin: 'curseforge'

group = 'com.matthewprenger.helpfixer'
archivesBaseName = 'HelpFixer'

if (System.getenv('BUILD_NUMBER') != null) {
    version += ".${System.getenv('BUILD_NUMBER')}"
} else {
    version += ".CUSTOM"
}

minecraft {
    version = "$minecraft_version-$forge_version"
    mappings = mappings_version
    runDir = 'run'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property 'description', project.description
    inputs.property 'url', project.url

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand([
                'version'    : project.version,
                'mcversion'  : project.minecraft.version,
                'description': project.description,
                'url'        : project.url
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

task sourcesJar(type: Jar, dependsOn: sourceMainJava) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task deobfJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.assemble.dependsOn sourcesJar, javadocJar, deobfJar

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

def manifestAttributes = [
        'Built-By'              : System.getProperty('user.name'),
        'Created-By'            : "${System.getProperty('java.version')} + (${System.getProperty('java.vendor')})",
        'Implementation-Version': project.version,
        'Implementation-Title'  : project.name,
        'Minecraft-Version'     : project.minecraft.version,
        'Forge-Version'         : forge_version
]

tasks.withType(Jar) { task -> task.manifest.mainAttributes(manifestAttributes) }

tasks.withType(JavaCompile) { task -> task.options.encoding = 'UTF-8' }

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.name
                    description project.description
                    url project.url
                    inceptionYear project.inceptionYear
                    packaging 'jar'
                    scm {
                        url 'https://github.com/matthewprenger/HelpFixer'
                        connection 'scm:git:git://github.com/matthewprenger/HelpFixer.git'
                        developerConnection 'scm:git:git@github.com:matthewprenger/HelpFixer.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/matthewprenger/HelpFixer/issues'
                    }
                    licenses {
                        license {
                            name 'DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE'
                            url 'http://www.wtfpl.net/txt/copying/'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'matthewprenger'
                            name 'Matthew Prenger'
                            roles { role 'developer' }
                        }
                    }
                }
            }

            artifact jar
            artifact sourcesJar
            artifact javadocJar
            artifact deobfJar
        }
    }

    repositories {
        if (project.hasProperty('forgemaven_user') && project.hasProperty('forgemaven_password')) {
            maven {
                credentials {
                    username project.forgemaven_user
                    password project.forgemaven_password
                }
                url 'http://files.minecraftforge.net/maven/manage/upload'
            }
        } else {
            maven { url file('repo') }
        }
    }
}

curse {
    mustRunAfter 'publish'
    onlyIf { return project.hasProperty('curseforge_key') }

    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }
    projectId = 223797
    changelog = project.hasProperty('changelog') ? project.changelog : ''
    releaseType = 'release'
}